# Maintainer: Aleksey Filippov <sarum9in@gmail.com>
_pkgname=lxc
pkgname=lxc204
pkgver=2.0.4
pkgrel=1
epoch=1
pkgdesc="LXC is a userspace interface for the Linux kernel containment features."
arch=('i686' 'x86_64')
url="https://linuxcontainers.org/lxc/introduction/"
license=('LGPL2')
depends=('python' 'libcap' 'rsync')
makedepends=()
conflicts=('lxc')
source=("http://linuxcontainers.org/downloads/$_pkgname-${pkgver}.tar.gz"{,.asc})
validpgpkeys=('602F567663E593BCBD14F338C638974D64792D67')
noextract=()
md5sums=('28db4dbacf860ae742138c0ed8dbf14c'
         'SKIP')

prepare() {
  cd "$srcdir/$_pkgname-$pkgver"
  sed -ri 's|-Werror||g' configure.ac
  sed -ri 's|\$\{prefix\}/||g' lxc.pc.in
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --disable-doc \
    --disable-api-docs \
    --disable-apparmor \
    --enable-capabilities
  make $MAKEFLAGS
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
