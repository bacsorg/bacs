# Maintainer: Aleksey Filippov <sarum9in@gmail.com>
pkgname='bacs.problem-git'
pkgver=0.0.0
pkgrel=1
pkgdesc="BACS problem importer library"
arch=('i686' 'x86_64')
url="https://github.com/bacsorg/problem"
license=('GPL')
groups=()
depends=(
  'bunsan.pm-git'
  'bunsan.protobuf-git'
  'bacs.common-git'
)
makedepends=(
  'bunsan.cmake-git'
  'git'
  'boost'
  'turtle-git'
)
provides=()
conflicts=('bunsan.bacs.problem-git')
replaces=('bunsan.bacs.problem-git')
backup=()
options=()
install=
source=("$pkgname::git+https://github.com/bacsorg/problem.git")
noextract=()
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/$pkgname"
  rm -rf build && mkdir build && cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
  make $MAKEFLAGS
}

check() {
  cd "$srcdir/$pkgname/build"
  make test ARGS=--output-on-failure
}

package() {
  cd "$srcdir/$pkgname/build"
  make DESTDIR="$pkgdir/" install
}
